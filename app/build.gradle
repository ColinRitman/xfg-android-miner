apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 31
    buildToolsVersion '30.0.3'

    compileOptions {
        sourceCompatibility '1.8'
        targetCompatibility '1.8'
    }
    
    kotlinOptions {
        jvmTarget = '1.8'
    }

    defaultConfig {
        applicationId "xfg.androidminer"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 4
        versionName "4.1"
        buildConfigField "long", "BUILD_TIME", System.currentTimeMillis() + "L"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = false
    }

    signingConfigs {
        release {
            if (project.hasProperty('MYAPP_RELEASE_STORE_FILE')) {
                storeFile file(MYAPP_RELEASE_STORE_FILE)
                storePassword MYAPP_RELEASE_STORE_PASSWORD
                keyAlias MYAPP_RELEASE_KEY_ALIAS
                keyPassword MYAPP_RELEASE_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false // Disable it for now, problem while fetching Json data for Pool API
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (project.hasProperty('MYAPP_RELEASE_STORE_FILE')) {
                signingConfig signingConfigs.release
            }
        }
    }
}

dependencies {
    //noinspection GradleDependency
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3' // Keep the version 1.1.3
    //noinspection GradleDependency
    implementation 'com.google.android.material:material:1.1.0' // Keep the version 1.1.0
    implementation('com.github.KingsMentor:MobileVisionBarcodeScanner:2.0.0') {
        exclude group: 'com.tbruyelle.rxpermissions2', module: 'rxpermissions'
    }
    implementation 'com.github.tbruyelle:RxPermissions:0.12' // Override broken transitive dependency
    //noinspection GradleDependency
    implementation 'androidx.appcompat:appcompat:1.1.0' // Keep the version 1.1.0
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'org.ocpsoft.prettytime:prettytime:4.0.2.Final'
    implementation 'com.android.volley:volley:1.2.1'
    implementation 'com.github.anastr:speedviewlib:1.6.1'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0'
    
    // Test dependencies
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:3.12.4'
}
